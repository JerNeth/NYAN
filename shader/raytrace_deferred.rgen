#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#include "raycommon.glsl"
#include "bufferReferences.glsl"
#include "structs.h"
#include "bindlessLayouts.glsl"
#include "common.glsl"
#include "extracts.glsl"
#include "lighting.glsl"
#include "gbuffer.glsl"
#include "raytrace_lighting.glsl"

layout(std430, push_constant) uniform PushConstants
{
    uint accBinding;
	uint sceneBinding;
	uint meshBinding;

    uint albedoBinding;
    uint albedoSampler;
    uint normalBinding;
    uint normalSampler;
    uint pbrBinding;
    uint pbrSampler;
    uint depthBinding;
    uint depthSampler;
    uint stencilBinding;
    uint stencilSampler;
    uint diffuseImageBinding;
    uint specularImageBinding;
} constants;
layout(set = 0, binding = 5) uniform accelerationStructureEXT accelerationStructures[ACC_COUNT];

//layout(location = 1) rayPayloadEXT float visibility;


float light_visibility(in ShadingData shadingData, in vec3 dir, in float tMax) 
{
    visibility = 0.f;
    uint  rayFlags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsSkipClosestHitShaderEXT;
    float tMin     = 0.01;
	traceRayEXT(accelerationStructures[constants.accBinding], // acceleration structure
            rayFlags,       // rayFlags
            0xFF,           // cullMask
            0,              // sbtRecordOffset
            0,              // sbtRecordStride
            0,              // missIndex
            shadingData.worldPos.xyz + shadingData.shadingNormal * 0.02, // ray origin
            tMin,           // ray min range
            dir.xyz,  // ray direction
            tMax,           // ray max range
            1               // payload (location = 0)
        );
    return visibility;
    //return 1.0f;
}
//const float minLight = 1e-5;
void point_lights(in Scene scene, in ShadingData shadingData, inout vec3 diffuse, inout vec3 specular) 
{
    for(int i = 0; i < maxNumPointLights; i++) 
    {
        PointLight light = scene.pointLights[i];
        vec3 lightDir = light.pos.xyz - shadingData.worldPos;
        float dist = length(lightDir);
        if(dist > light.attenuationDistance)
            continue;

        lightDir /= dist;
        float lightShadow = light_visibility(shadingData, lightDir,dist);
        if(lightShadow <= minLight )
            continue;
        //Falloff borrowed from Real Shading in Unreal Engine 4, Brian Karis 2013 SIGGRAPH
        float distRatio = dist / light.attenuationDistance;
        distRatio *= distRatio;
        distRatio *= distRatio;
        float falloff = min(max(1.f - distRatio, 0.0f), 1.0f);
        falloff *= falloff;
        falloff *= 1.f / (dist * dist + 1.f);
        LightData lightData;
        lightData.dir = lightDir;
        lightData.intensity = light.intensity * falloff;
        lightData.color = light.color.rgb;
        //calc_light(lightData, shadingData, diffuse, specular);
        diffuse_light(lightData, shadingData, diffuse);
    }
}

void direct_lighting(in Scene scene, in ShadingData shadingData, out vec3 diffuse, out vec3 specular)
{
    diffuse = vec3(0.f);
    specular = vec3(0.f);
    if(scene.dirLight.enabled > 0) 
    {
        float lightShadow = light_visibility(shadingData, -scene.dirLight.dir, 1e27f);
        if(lightShadow > minLight) {
            LightData lightData;
            lightData.dir = -scene.dirLight.dir;
            lightData.intensity = scene.dirLight.intensity;
            lightData.color = scene.dirLight.color;
            //calc_light(lightData, shadingData, diffuse, specular);
            diffuse_light(lightData, shadingData, diffuse);
        }
    }
    if(scene.numPointLights > 0)
    {
        point_lights(scene, shadingData, diffuse, specular);
    }
}

void main() 
{    
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);

    Scene scene = scenes[constants.sceneBinding].scene;
	DirectionalLight light = scene.dirLight;
    
    uint stencil = texelFetch( usampler2D(utextures2D[constants.stencilBinding], samplers[constants.stencilSampler]), ivec2(gl_LaunchIDEXT.xy), 0).x;

    if(stencil != 0) {
        GBufferData gBufferData;
        //#define SHOW_NORMALS
        gBufferData.data0 = texture(sampler2D(textures2D[constants.albedoBinding], samplers[constants.albedoSampler]), inUV);
        gBufferData.data1 = texture(sampler2D(textures2D[constants.normalBinding], samplers[constants.normalSampler]), inUV);
        gBufferData.data2 = texture(sampler2D(textures2D[constants.pbrBinding], samplers[constants.pbrSampler]), inUV);
        MaterialData materialData = decode_gbuffer_data(gBufferData);
    #ifndef SHOW_NORMALS
        float depth = texture(sampler2D(textures2D[constants.depthBinding], samplers[constants.depthBinding]), inUV).x;


        vec4 clipSpacePos = vec4(inUV * 2.0 - 1.0, depth, 1.0);
        vec4 viewSpacePos = scene.invProj * clipSpacePos;
        viewSpacePos /= viewSpacePos.w;
        vec4 worldSpacePos = scene.invView * viewSpacePos;
        
        //vec4 diffuseAccum  = vec4(0.f, 0.f, 0.f, 1.f);
        //vec4 specularAccum = vec4(0.f, 0.f, 0.f, 1.f);
        vec3 diffuseAccum  = vec3(0.f, 0.f, 0.f);
        vec3 specularAccum = vec3(0.f, 0.f, 0.f);
        ShadingData shadingData;
        shadingData.albedo = materialData.albedo.xyz;
        shadingData.alpha = materialData.roughness;
        shadingData.worldPos = worldSpacePos.xyz;
        shadingData.outLightDir = normalize(get_viewer_pos(scene) - shadingData.worldPos.xyz);
        shadingData.metalness = materialData.metalness;
        shadingData.shadingNormal = materialData.shadingNormal;
        direct_lighting(scene, shadingData, diffuseAccum.xyz, specularAccum.xyz);
        //diffuse_direct_lighting(accelerationStructures[constants.accBinding], scene, shadingData, diffuseAccum, specularAccum);
        
        specularAccum.xyz += materialData.emissive.xyz;// * (1.f / (3.1415926f));
        //specularAccum.xyz = vec3(0.f);
        //imageStore(imagesRGBA16F[constants.diffuseImageBinding], ivec2(gl_LaunchIDEXT.xy), diffuseAccum);
        //imageStore(imagesRGBA16F[constants.specularImageBinding], ivec2(gl_LaunchIDEXT.xy), specularAccum);
        imageStore(imagesRGBA16F[constants.diffuseImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(diffuseAccum, 1.f));
        imageStore(imagesRGBA16F[constants.specularImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(specularAccum, 1.f));
    #else
        //imageStore(imagesRGBA16F[constants.specularImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(materialData.albedo.xyz, 1.f));
        imageStore(imagesRGBA16F[constants.specularImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(materialData.shadingNormal, 1.f));
        imageStore(imagesRGBA16F[constants.diffuseImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(0));
    #endif
    } else {
        imageStore(imagesRGBA16F[constants.diffuseImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(0));
        imageStore(imagesRGBA16F[constants.specularImageBinding], ivec2(gl_LaunchIDEXT.xy), vec4(scene.skyLight.color, 1.f));
    }
}
