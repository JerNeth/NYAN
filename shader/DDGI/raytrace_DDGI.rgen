#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#include "../raycommon.glsl"
#include "../bufferReferences.glsl"
#include "../structs.h"
#include "../bindlessLayouts.glsl"
#include "../common.glsl"
#include "../lighting.glsl"
#include "ddgi_common.glsl"
#include "ddgi_sampling.glsl"

layout(location = 0) rayPayloadEXT PackedPayload pld;
//layout(location = 0) rayPayloadEXT Payload payload;
layout(location = 1) rayPayloadEXT float shadowed;

layout(set = 0, binding = 5) uniform accelerationStructureEXT accelerationStructures[ACC_COUNT];


layout(std430, push_constant) uniform PushConstants
{
	uint accBinding;
	uint sceneBinding;
	uint meshBinding;
	uint ddgiBinding;
	uint ddgiCount;
	uint ddgiIndex;
	uint renderTarget;
	vec4 col;
	vec4 randomRotation;
} constants;


float light_visibility(in ShadingData shadingData, in vec3 dir, in float tMax) 
{
    shadowed = 0.f;
    uint  rayFlags = gl_RayFlagsOpaqueEXT | gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsSkipClosestHitShaderEXT;
    float tMin     = 0.0;
	traceRayEXT(accelerationStructures[constants.accBinding], // acceleration structure
            rayFlags,       // rayFlags
            0xFF,           // cullMask
            0,              // sbtRecordOffset
            0,              // sbtRecordStride
            1,              // missIndex
            shadingData.worldPos.xyz, // ray origin
            tMin,           // ray min range
            dir.xyz,  // ray direction
            tMax,           // ray max range
            1               // payload (location = 0)
        );
    return shadowed;
    //return 1.0f;
}
const float minLight = 1e-5;
vec3 diffuse_point_lights(in Scene scene, in ShadingData shadingData) 
{
    vec3 diffuse = vec3(0.f);
    for(int i = 0; i < scene.numPointLights; i++) 
    {
        PointLight light = scene.pointLights[i];
        vec3 lightDir = light.pos - shadingData.worldPos;
        float dist = length(lightDir);
        if(dist > light.attenuationDistance)
            continue;

        lightDir /= dist;
        float lightShadow = light_visibility(shadingData, lightDir,dist);
        if(lightShadow <= minLight )
            continue;
        //Falloff borrowed from Real Shading in Unreal Engine 4, Brian Karis 2013 SIGGRAPH
        float distRatio = dist / light.attenuationDistance;
        distRatio *= distRatio;
        distRatio *= distRatio;
        float falloff = min(max(1.f - distRatio, 0.0f), 1.0f);
        falloff *= falloff;
        falloff *= 1.f / (dist * dist + 1.f);
        LightData lightData;
        lightData.dir = lightDir;
        lightData.intensity = light.intensity;
        lightData.color = light.color;
        diffuse += diffuse_light(lightData, shadingData) * falloff;
        
    }
    return diffuse;
}

vec3 diffuse_direct_lighting(in Scene scene, in ShadingData shadingData)
{
    vec3 diffuse = vec3(0.f);
    //Only shade dielectrics
    if(shadingData.metalness >= 1) {
        return vec3(0.f);
    }
    if(scene.dirLight.enabled) 
    {
        float lightShadow = light_visibility(shadingData, -scene.dirLight.dir, 1e27f);
        if(lightShadow > minLight) {
            LightData lightData;
            lightData.dir = scene.dirLight.dir;
            lightData.intensity = scene.dirLight.intensity;
            lightData.color = scene.dirLight.color;
            diffuse += diffuse_light(lightData, shadingData);
        }
    }
    if(scene.numPointLights > 0)
    {
        //diffuse += diffuse_point_lights(scene, shadingData);
    }
    return diffuse;
}

vec3 diffuse_indirect_lighting(in Payload payload)
{
	//TODO
	//Evaluate DDGI
	return vec3(0.f);
}

void main()
{
    //gl_LaunchIDEXT.xy;
    Scene scene = scenes[constants.sceneBinding].scene;
	uint rayIdx = gl_LaunchIDEXT.x;
	uint probeIdx = gl_LaunchIDEXT.y;
	
	DDGIVolume volume = ddgiVolumes[constants.ddgiBinding].volume[constants.ddgiIndex];

	vec3 probeCords = get_probe_coordinates(probeIdx, volume);
	vec3 direction = quaternion_rotate(constants.randomRotation, get_ray_direction(rayIdx, volume));

    uint  rayFlags = gl_RayFlagsOpaqueEXT;
    float tMin     = 0.001;
    float tMax     = volume.maxRayDistance;
	traceRayEXT(accelerationStructures[constants.accBinding], // acceleration structure
          rayFlags,       // rayFlags
          0xFF,           // cullMask
          0,              // sbtRecordOffset
          0,              // sbtRecordStride
          0,              // missIndex
          probeCords.xyz, // ray origin
          tMin,           // ray min range
          direction.xyz,  // ray direction
          tMax,           // ray max range
          0               // payload (location = 0)
     );
	 Payload payload = unpack_payload(pld);

	 vec3 diffuse = vec3(0.f);
	 vec3 irradiance = vec3(0.f); //Indirect

	 float hitDistance = payload.hitT;

	 if(payload.hitkind == gl_HitKindBackFacingTriangleEXT) 
	 {
		hitDistance *= -0.2;
	 }
	 if(payload.hitkind == HitDDGIMiss) 
	 {
		diffuse = payload.albedo;
		//If miss hitDist = 1e27f for 32-float, less for 16-float 65519, might reconsider 16-bit since rays are filtered
		hitDistance = 65519.f; 
	 } 
	 else {
        ShadingData shadingData;
        shadingData.albedo = payload.albedo;
        shadingData.alpha = payload.roughness * payload.roughness;
        shadingData.outLightDir = -direction;
        shadingData.metalness = payload.metallic;
        shadingData.shadingNormal = payload.shadingNormal;
        shadingData.worldPos = payload.worldPos;
        //High compile time here, probably due to shadow rays
		diffuse += diffuse_direct_lighting(scene, shadingData);
		diffuse += diffuse_indirect_lighting(payload);
     }

	 imageStore(imagesRGBA16F[constants.renderTarget], ivec2(gl_LaunchIDEXT.xy), vec4(diffuse, hitDistance));
}